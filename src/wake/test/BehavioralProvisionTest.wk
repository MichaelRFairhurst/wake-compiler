#>
# Source Code for the Original Compiler for the
# Programming Language Wake
#
# BehavioralProvisionTest.wk
#
# Licensed under the MIT license
# See LICENSE.TXT for details
#
# Author: Michael Fairhurst
# Revised By:
#
##

import Asserts;

every BehavioralProvisionTest is:

	with Num provideme = 0;
	with Text providemetext = "";

	provides
		Num:ProvideMe <- { return provideme; },
		Text:ProvideMe <- { return providemetext; },
		Num:changeProperties <- { provideme = 2; providemetext = "hello"; return 4; },
		Num:ProvideProvideMe <- { return Num:ProvideMe from this; },
		Text:ProvideProvideMe <- { return Text:ProvideMe from this; },
		Text:ProvideArg1 <- (Text, $Text) { return Text; },
		Text:ProvideArg2 <- (Text, $Text) { return $Text; },
		Text:ProvideAdded <- (Text, $Text) { return Text + $Text; };

	~[ test get property via provision/declaration after set ]~(Asserts) {
		provideme = 10;
		var Num:ProvideMe from this;
		Asserts.that(Num)Equals(10);
	}

	~[ test get property via provision (no declaration) after set ]~(Asserts) {
		provideme = 7;
		Asserts.that(Num:ProvideMe from this)Equals(7);
	}

	~[ test get text property via provision/declaration after set ]~(Asserts) {
		providemetext = "Heey";
		var Text:ProvideMe from this;
		Asserts.that(Text)Equals("Heey");
	}

	~[ test get text property via provision (no declaration) after set ]~(Asserts) {
		providemetext = "Yo";
		Asserts.that(Text:ProvideMe from this)Equals("Yo");
	}

	~[ test get property via provision/declaration calls other provision after set ]~(Asserts) {
		provideme = 10;
		var Num:ProvideProvideMe from this;
		Asserts.that(Num)Equals(10);
	}

	~[ test get property via provision (no declaration) calls other provision after set ]~(Asserts) {
		provideme = 7;
		Asserts.that(Num:ProvideProvideMe from this)Equals(7);
	}

	~[ test get text property via provision/declaration calls other provision after set ]~(Asserts) {
		providemetext = "Heey";
		var Text:ProvideProvideMe from this;
		Asserts.that(Text)Equals("Heey");
	}

	~[ test get text property via provision (no declaration) calls other provision after set ]~(Asserts) {
		providemetext = "Yo";
		Asserts.that(Text:ProvideProvideMe from this)Equals("Yo");
	}

	~[ test set properties in a provision ]~(Asserts) {
		Num:changeProperties from this;
		var Num:ProvideMe from this;
		Asserts.that(provideme)Equals(2);
		Asserts.that(providemetext)Equals("hello");
	}

	~[ test provide arg 1 gets arg 1 and ignores arg 2 ]~(Asserts) {
		Asserts.that(Text:ProvideArg1("hey", "man") from this)Equals("hey");
	}

	~[ test provide arg 2 gets arg 2 and ignores arg 1 ]~(Asserts) {
		Asserts.that(Text:ProvideArg2("hey", "man") from this)Equals("man");
	}

	~[ test provide args added ]~(Asserts) {
		Asserts.that(Text:ProvideAdded("hey", "man") from this)Equals("heyman");
	}
