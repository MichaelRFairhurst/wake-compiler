#>
# Source Code for the Original Compiler for the
# Programming Language Wake
#
# OptionalTypeTest.wk
#
# Licensed under the MIT license
# See LICENSE.TXT for details
#
# Author: Michael Fairhurst
# Revised By:
#
##

import Asserts;

@TestClass
every OptionalTypeTest is:

	@Test
	~[test optional int never set]~(Asserts) {
		var Num? = nothing;
		if Num exists {
			Asserts.fail("Num doesn't exist");
		}
	}

	@Test
	~[test optional int set]~(Asserts) {
		var Num? = 5;
		if Num exists {
			Asserts.that(Num)Equals(5);
			return;
		}
		Asserts.fail("Num should have existed");
	}

	@Test
	~[test optional int not set will hit else]~(Asserts) {
		var Num? = nothing;
		if Num exists {} else {
			return;
		}
		Asserts.fail("We didn't hit the else block");
	}

	@Test
	~[test optional int set wont hit else]~(Asserts) {
		var Num? = 5;
		if Num exists {} else {
			Asserts.fail("Should not have hit else block");
		}

	}

	@Test
	~[test zero and empty string are not nothing]~(Asserts) {
		var Num? = 0;
		var Text? = "";
		if Num exists { } else { Asserts.fail("0 is read as nothing"); }
		if Text exists { } else { Asserts.fail("\"\" is read as nothing"); }
		Asserts.that(Num != nothing);
		Asserts.that(Text != nothing);
	}
